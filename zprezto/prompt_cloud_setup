pmodload 'helper'

function prompt_cloud_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
      git-info
  fi
}

function prompt_cloud_preview {
    prompt_preview_theme 'cloud'
}

function prompt_cloud_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # set the prompt character
  prompt_char='λ'

  # assign colors.
  primary_color='cyan'
  secondary_color='green'

  # load required functions.
  autoload -Uz add-zsh-hook

  # add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_cloud_precmd

  # set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:dirty' format "%%B%F{$secondary_color}]%f%%b %F{yellow}⚡%f"
  zstyle ':prezto:module:git:info:clean' format "%B%F{$secondary_color}]%f%b"
  zstyle ':prezto:module:git:info:branch' format "%%B%F{$secondary_color}[%f%%b%%B%F{$primary_color}%b%f%%b"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b%C%D' 'rprompt' ''

  # define prompts.
  PROMPT='%B%F{$secondary_color}[%c]%f%b${git_info:+${(e)git_info[prompt]}} %B%F{$primary_color}${prompt_char}%f%b '
  RPROMPT='%B$node_info[version]%B$ruby_info[version]'
}

prompt_cloud_setup "$@"
